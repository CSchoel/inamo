<!DOCTYPE html>
<html lang="en"><head><meta charset="UTF-8"/><meta name="viewport" content="width=device-width, initial-scale=1.0"/><title>Ion concentrations · InaMo</title><link href="https://fonts.googleapis.com/css?family=Lato|Roboto+Mono" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.11.2/css/fontawesome.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.11.2/css/solid.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.11.2/css/brands.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/KaTeX/0.11.1/katex.min.css" rel="stylesheet" type="text/css"/><script>documenterBaseURL="../.."</script><script src="https://cdnjs.cloudflare.com/ajax/libs/require.js/2.3.6/require.min.js" data-main="../../assets/documenter.js"></script><script src="../../siteinfo.js"></script><script src="../../../versions.js"></script><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-dark.css" data-theme-name="documenter-dark"/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-light.css" data-theme-name="documenter-light" data-theme-primary/><script src="../../assets/themeswap.js"></script></head><body><div id="documenter"><nav class="docs-sidebar"><div class="docs-package-name"><span class="docs-autofit">InaMo</span></div><form class="docs-search" action="../../search/"><input class="docs-search-query" id="documenter-search-query" name="q" type="text" placeholder="Search docs"/></form><ul class="docs-menu"><li><a class="tocitem" href="../../">InaMo</a></li><li><span class="tocitem">Models</span><ul><li><a class="tocitem" href="../cells/">Full cell models</a></li><li><a class="tocitem" href="../membrane/">Membrane model</a></li><li><a class="tocitem" href="../channels/">Ion channels</a></li><li class="is-active"><a class="tocitem" href>Ion concentrations</a><ul class="internal"><li><a class="tocitem" href="#Interfaces"><span>Interfaces</span></a></li><li><a class="tocitem" href="#Basic-components"><span>Basic components</span></a></li><li><a class="tocitem" href="#Ca2-handling"><span>Ca2+ handling</span></a></li></ul></li><li><a class="tocitem" href="../pumps/">Ion pumps</a></li><li><a class="tocitem" href="../experimental-methods/">Experimental methods</a></li><li><a class="tocitem" href="../examples/">Examples (full runnable models)</a></li></ul></li><li><span class="tocitem">Modelica features</span><ul><li><a class="tocitem" href="../../extends/">Ineritance in InaMo</a></li><li><a class="tocitem" href="../../outer/">Outer parameters</a></li><li><a class="tocitem" href="../../replaceable/">Replaceable functions</a></li><li><a class="tocitem" href="../../acausal/">Acausal connectors</a></li></ul></li><li><a class="tocitem" href="../../unit-tests/">Unit tests</a></li><li><a class="tocitem" href="../../ci/">Continuous integration</a></li><li><a class="tocitem" href="../../reconstruction/">Plot reconstruction</a></li></ul><div class="docs-version-selector field has-addons"><div class="control"><span class="docs-label button is-static is-size-7">Version</span></div><div class="docs-selector control is-expanded"><div class="select is-fullwidth is-size-7"><select id="documenter-version-selector"></select></div></div></div></nav><div class="docs-main"><header class="docs-navbar"><nav class="breadcrumb"><ul class="is-hidden-mobile"><li><a class="is-disabled">Models</a></li><li class="is-active"><a href>Ion concentrations</a></li></ul><ul class="is-hidden-tablet"><li class="is-active"><a href>Ion concentrations</a></li></ul></nav><div class="docs-right"><a class="docs-edit-link" href="https://github.com/CSchoel/inamo/blob/master/docs/src/models/concentrations.md" title="Edit on GitHub"><span class="docs-icon fab"></span><span class="docs-label is-hidden-touch">Edit on GitHub</span></a><a class="docs-settings-button fas fa-cog" id="documenter-settings-button" href="#" title="Settings"></a><a class="docs-sidebar-button fa fa-bars is-hidden-desktop" id="documenter-sidebar-button" href="#"></a></div></header><article class="content" id="documenter-page"><h1 id="Ion-concentrations"><a class="docs-heading-anchor" href="#Ion-concentrations">Ion concentrations</a><a id="Ion-concentrations-1"></a><a class="docs-heading-anchor-permalink" href="#Ion-concentrations" title="Permalink"></a></h1><html>
  <p>The models in this package mainly concern the Ca2+ handling by the
  sarcoplasmic reticulum and various Ca2+ buffers, but the package
  InaMo.Concentrations.Interfaces also contains some general models that
  can be used in ion currents, whose associated ion concentrations are
  variable.</p>
</htmL><h2 id="Interfaces"><a class="docs-heading-anchor" href="#Interfaces">Interfaces</a><a id="Interfaces-1"></a><a class="docs-heading-anchor-permalink" href="#Interfaces" title="Permalink"></a></h2><pre><code class="language-modelica">connector SubstanceSite &quot;general connector for transferring substances&quot;
  SI.AmountOfSubstance amount(nominal = 1e-21) &quot;amount of substance&quot;;
  flow SI.MolarFlowRate rate(nominal = 1e-17) &quot;molar flow rate of substance&quot;;
  annotation(
    Icon(graphics = {Ellipse(origin = {0, 0}, extent = {{-100, -100}, {100, 100}}, fillColor = {145, 204, 255}, fillPattern = FillPattern.Solid)}));
end SubstanceSite;</code></pre>
    <p>This is a base class for models that represent a substance transfer
    from one compartment to another.</p>
    <p>If both <code>src</code> and <code>dst</code> are also connected to
    an instance of InaMo.Concentrations.Basic.Compartment, models inheriting
    from this subclass will ensure conservation of mass between these
    compartments.</p>
  <pre><code class="language-modelica">partial model SubstanceTransport &quot;base model for transport of an amount of substance between two compartments&quot;
  InaMo.Concentrations.Interfaces.SubstanceSite dst &quot;destination of transport (for positive rate)&quot; annotation(
    Placement(transformation(extent = {{-15, 85}, {15, 115}})));
  InaMo.Concentrations.Interfaces.SubstanceSite src &quot;source of transport (for positive rate)&quot; annotation(
    Placement(transformation(extent = {{-15, -115}, {15, -85}})));
  SI.MolarFlowRate rate &quot;rate of change in substance amount&quot;;
  // NOTE: due to a bug in OpenModelica, this currently has to come last
equation
  src.rate + dst.rate = 0 &quot;conservation of mass&quot;;
  src.rate = rate;
  annotation(
    Documentation(info = &quot;&lt;html&gt;
    &lt;p&gt;This is a base class for models that represent a substance transfer
    from one compartment to another.&lt;/p&gt;
    &lt;p&gt;If both &lt;code&gt;src&lt;/code&gt; and &lt;code&gt;dst&lt;/code&gt; are also connected to
    an instance of InaMo.Concentrations.Basic.Compartment, models inheriting
    from this subclass will ensure conservation of mass between these
    compartments.&lt;/p&gt;
  &lt;/html&gt;&quot;),
    Icon(coordinateSystem(extent = {{-100, -100}, {100, 100}}, preserveAspectRatio = false), graphics = {Line(origin = {-100, 2145.04}, points = {{100.05, -2157.01}, {100.05, -2133.08}}, thickness = 0.5), Line(origin = {-100, 2145.04}, points = {{94.67, -2138.60}, {100.05, -2133.08}, {105.71, -2138.60}}, thickness = 0.5)}));
end SubstanceTransport;</code></pre><pre><code class="language-modelica">partial model InactiveChemicalTransport &quot;substance transport along chemical concentration gradient&quot;
  extends InaMo.Concentrations.Interfaces.SubstanceTransport;
  parameter SI.Volume vol_src &quot;volume of source compartment&quot;;
  parameter SI.Volume vol_dst &quot;volume of destination compartment&quot;;
  parameter SI.Volume vol_trans = min(vol_src, vol_dst) &quot;volume of substance that is transferred between compartments over 1/coeff seconds&quot;;
  Real coeff(quantity = &quot;reaction rate coefficient&quot;, unit = &quot;1/s&quot;) &quot;coefficient of transport&quot;;
equation
  rate = coeff * (src.amount / vol_src - dst.amount / vol_dst) * vol_trans;
end InactiveChemicalTransport;</code></pre><pre><code class="language-modelica">model ElectricalIonTransport &quot;transport of ions across a boundary due to a current&quot;
  extends InaMo.Concentrations.Interfaces.SubstanceTransport;
  extends InaMo.Concentrations.Basic.ECAdapter(i = i_ion);
  outer SI.Current i_ion &quot;current responsible for moving ions&quot;;
end ElectricalIonTransport;</code></pre>
  <p>This model allows to define transmembrane currents, which only change
  the intracellular concentration but keep the extracellular concentration
  constant.</p>
  <p>It should not be used on its own.
  Instead, InaMo.Concentrations.Interfaces.TransmembraneCaFlow and similar
  models should be used in order to clearly define the type of ion that is
  transported.</p>
<pre><code class="language-modelica">model EITransportConst &quot;ElectricalIonTransport with constant destination concentration&quot;
  InaMo.Concentrations.Interfaces.ElectricalIonTransport trans;
  InaMo.Concentrations.Basic.ConstantConcentration con;
equation
  connect(con.substance, trans.dst);
  annotation(
    Documentation(info = &quot;&lt;html&gt;
  &lt;p&gt;This model allows to define transmembrane currents, which only change
  the intracellular concentration but keep the extracellular concentration
  constant.&lt;/p&gt;
  &lt;p&gt;It should not be used on its own.
  Instead, InaMo.Concentrations.Interfaces.TransmembraneCaFlow and similar
  models should be used in order to clearly define the type of ion that is
  transported.&lt;/p&gt;
&lt;/html&gt;&quot;));
end EITransportConst;</code></pre>
  <p>This model can be used via an extends clause in models which define an
  ion current for a variable intracellular Ca2+ concentration.
  The inheriting model only needs to define an
  <code>inner SI.Current i_ion</code> to match the <code>outer</code>
  definition in InaMo.Concentrations.Interfaces.ElectricalIonTransport.</p>
<pre><code class="language-modelica">model TransmembraneCaFlow &quot;mixin for components that transport Ca2+ ions from or to the extracellular compartment&quot;
  extends InaMo.Concentrations.Interfaces.EITransportConst(trans(n = n_ca, z = 2), con.c_const = ca_ex);
  SubstanceSite ca &quot;intracellular Ca2+ concentration&quot; annotation(
    Placement(visible = true, transformation(origin = {35, -100}, extent = {{-17, -17}, {17, 17}})));
  parameter Real n_ca = 1 &quot;stoichiometric ratio of transport&quot;;
  outer parameter SI.Concentration ca_ex &quot;extracellular concentration of Ca2+ ions&quot;;
equation
  connect(ca, trans.src);
  annotation(
    Documentation(info = &quot;&lt;html&gt;
  &lt;p&gt;This model can be used via an extends clause in models which define an
  ion current for a variable intracellular Ca2+ concentration.
  The inheriting model only needs to define an
  &lt;code&gt;inner SI.Current i_ion&lt;/code&gt; to match the &lt;code&gt;outer&lt;/code&gt;
  definition in InaMo.Concentrations.Interfaces.ElectricalIonTransport.&lt;/p&gt;
&lt;/html&gt;&quot;));
end TransmembraneCaFlow;</code></pre>
  <p>Although this model only contains a single inner parameter definition,
  its name makes its use more intuitive than copying said parameter definition
  into all models that need it.</p>
<pre><code class="language-modelica">partial model CaConst &quot;model used to set all parameters required to keep intracellular Ca2+ constant&quot;
  inner parameter SI.Volume v_sub = 1;
  annotation(
    Documentation(info = &quot;&lt;html&gt;
  &lt;p&gt;Although this model only contains a single inner parameter definition,
  its name makes its use more intuitive than copying said parameter definition
  into all models that need it.&lt;/p&gt;
&lt;/html&gt;&quot;));
end CaConst;</code></pre><pre><code class="language-modelica">partial model NoACh &quot;model used to set all parameters required to deactivate ACh influence&quot;
  inner parameter Boolean use_ach = false;
  inner parameter SI.Concentration ach = 0;
end NoACh;</code></pre><h2 id="Basic-components"><a class="docs-heading-anchor" href="#Basic-components">Basic components</a><a id="Basic-components-1"></a><a class="docs-heading-anchor-permalink" href="#Basic-components" title="Permalink"></a></h2>
    <p>This model can be used to combine multiple influences on a single
    substance concentration/amount in a compartment.
    Individual effects can be introduced by adding a connection to the
    connector <code>substance</code>.</p>
  <pre><code class="language-modelica">model Compartment &quot;compartment that holds an ion concentration&quot;
  extends InaMo.Icons.Compartment;
  InaMo.Concentrations.Interfaces.SubstanceSite substance &quot;substance in the compartment&quot; annotation(
    Placement(transformation(extent = {{-15, -115}, {15, -85}})));
  parameter SI.Volume vol &quot;volume of the compartment&quot;;
  parameter SI.Concentration c_start = 1 &quot;initial value of concentration&quot;;
  SI.Concentration con = substance.amount / vol &quot;concentration of substance in compartment&quot;;
initial equation
  substance.amount = c_start * vol;
equation
  der(substance.amount) = substance.rate;
  annotation(
    Documentation(info = &quot;&lt;html&gt;
    &lt;p&gt;This model can be used to combine multiple influences on a single
    substance concentration/amount in a compartment.
    Individual effects can be introduced by adding a connection to the
    connector &lt;code&gt;substance&lt;/code&gt;.&lt;/p&gt;
  &lt;/html&gt;&quot;),
    Icon(graphics = {Text(origin = {-54, 67}, extent = {{104, -25}, {-2, 3}}, textString = &quot;%name&quot;)}));
end Compartment;</code></pre>
    <p>This model is similar to InaMo.Concentrations.Basic.Compartment, but
    ignores all rate changes introduced due to connections to
    <code>substance</code> in order to hold the concentration constant.</p>
  <pre><code class="language-modelica">model ConstantConcentration &quot;ion concentration with constant value&quot;
  extends InaMo.Icons.Compartment;
  InaMo.Concentrations.Interfaces.SubstanceSite substance annotation(
    Placement(transformation(extent = {{-15, -115}, {15, -85}})));
  parameter SI.Concentration c_const = 1 &quot;fixed concentration&quot;;
  parameter SI.Volume vol = 1 &quot;volume of the compartment&quot;;
equation
  substance.amount = c_const * vol;
  annotation(
    Documentation(info = &quot;&lt;html&gt;
    &lt;p&gt;This model is similar to InaMo.Concentrations.Basic.Compartment, but
    ignores all rate changes introduced due to connections to
    &lt;code&gt;substance&lt;/code&gt; in order to hold the concentration constant.&lt;/p&gt;
  &lt;/html&gt;&quot;),
    Icon(graphics = {Text(origin = {-54, 67}, extent = {{104, -25}, {-2, 3}}, textString = &quot;%name&quot;), Text(origin = {-81, 0}, rotation = -90, extent = {{-99, 10}, {99, -12}}, textString = &quot;%c_const mM&quot;)}));
end ConstantConcentration;</code></pre><pre><code class="language-modelica">model Diffusion &quot;simple linear diffusion with time constant&quot;
  extends InaMo.Concentrations.Interfaces.InactiveChemicalTransport;
  extends InaMo.Icons.Diffusion;
  parameter SI.Duration tau &quot;time constant of diffusion&quot;;
equation
  coeff = 1 / tau;
end Diffusion;</code></pre><pre><code class="language-modelica">model ReversibleAssociation &quot;reversible association reaction with stoichiometry 1:1:1&quot;
  extends InaMo.Icons.ReversibleAssociation;
  InaMo.Concentrations.Interfaces.SubstanceSite free &quot;free macromolecule&quot; annotation(
    Placement(transformation(origin = {-100, 50}, extent = {{-15, -15}, {15, 15}})));
  InaMo.Concentrations.Interfaces.SubstanceSite occupied &quot;occupied macromolecule&quot; annotation(
    Placement(transformation(origin = {100, 0}, extent = {{-15, -15}, {15, 15}})));
  InaMo.Concentrations.Interfaces.SubstanceSite ligand &quot;ligand&quot; annotation(
    Placement(transformation(origin = {-100, -50}, extent = {{-15, -15}, {15, 15}})));
  parameter Real k(unit = &quot;mol-1s-1&quot;) &quot;association constant&quot;;
  parameter Real kb(unit = &quot;s-1&quot;) &quot;dissociation constant&quot;;
protected
  SI.MolarFlowRate rate = k * ligand.amount * free.amount - kb * occupied.amount &quot;turnover rate&quot;;
equation
  free.rate = rate;
  occupied.rate = -rate;
  ligand.rate = rate;
end ReversibleAssociation;</code></pre><pre><code class="language-modelica">model Buffer &quot;buffer that only binds to a single ligand&quot;
  extends InaMo.Icons.Buffer;
  InaMo.Concentrations.Interfaces.SubstanceSite site &quot;binding site for ligand&quot; annotation(
    Placement(transformation(extent = {{-45, 57}, {-11, 91}})));
  parameter SI.AmountOfSubstance n_tot &quot;total amount of buffer&quot;;
  parameter Real f_start(unit = &quot;1&quot;) &quot;initial value for f&quot;;
  parameter Real k(unit = &quot;mol-1s-1&quot;) &quot;association constant&quot;;
  parameter Real kb(unit = &quot;s-1&quot;) &quot;dissociation constant&quot;;
  parameter SI.Volume vol = 1 &quot;volume of compartment in which buffer resides&quot;;
  ReversibleAssociation assoc(k = k, kb = kb) annotation(
    Placement(transformation(origin = {35, 0}, extent = {{-20, -20}, {20, 20}})));
  Compartment free(c_start = (1 - f_start) * n_tot / vol, vol = vol) annotation(
    Placement(transformation(origin = {24, 60}, extent = {{-20, -20}, {20, 20}})));
  Compartment occupied(c_start = f_start * n_tot / vol, vol = vol) annotation(
    Placement(transformation(origin = {80, 30}, extent = {{-20, -20}, {20, 20}})));
equation
  connect(assoc.ligand, site) annotation(
    Line(points = {{-28, 74}, {-28, -10}, {18, -10}}));
  connect(assoc.free, free.substance) annotation(
    Line(points = {{18, 10}, {6, 10}, {6, 30}, {24, 30}, {24, 40}}));
  connect(assoc.occupied, occupied.substance) annotation(
    Line(points = {{58, 0}, {80, 0}, {80, 8}}));
  annotation(
    Icon(graphics = {Text(origin = {-110, 0}, extent = {{-100, -10}, {100, 10}}, textString = &quot;%name&quot;, rotation = 90)}));
end Buffer;</code></pre><pre><code class="language-modelica">model Buffer2
  extends InaMo.Icons.Buffer;
  InaMo.Concentrations.Interfaces.SubstanceSite site_a &quot;binding site for ligand A&quot; annotation(
    Placement(transformation(extent = {{-45, 57}, {-11, 91}})));
  InaMo.Concentrations.Interfaces.SubstanceSite site_b &quot;binding site for ligand B&quot; annotation(
    Placement(transformation(extent = {{105, 85}, {135, 115}})));
  parameter SI.AmountOfSubstance n_tot &quot;total amount of buffer&quot;;
  parameter Real f_a_start(unit = &quot;1&quot;) &quot;initial value for f&quot;;
  parameter Real f_b_start(unit = &quot;1&quot;) &quot;initial value for f&quot;;
  parameter Real k_a(unit = &quot;mol-1s-1&quot;) &quot;association constant for binding to ligand A&quot;;
  parameter Real k_b(unit = &quot;mol-1s-1&quot;) &quot;association constant for binding to ligand B&quot;;
  parameter Real kb_a(unit = &quot;s-1&quot;) &quot;dissociation constant for binding to ligand A&quot;;
  parameter Real kb_b(unit = &quot;s-1&quot;) &quot;dissociation constant for binding to ligand B&quot;;
  parameter SI.Volume vol = 1 &quot;volume of compartment in which buffer resides&quot;;
  ReversibleAssociation assoc_a(k = k_a, kb = kb_a) annotation(
    Placement(transformation(origin = {-46, 10}, extent = {{-10, -10}, {10, 10}}, rotation = 180)));
  ReversibleAssociation assoc_b(k = k_b, kb = kb_b) annotation(
    Placement(transformation(origin = {46, 10}, extent = {{-10, 10}, {10, -10}})));
  Compartment free(c_start = (1 - f_a_start - f_b_start) * n_tot / vol, vol = vol) annotation(
    Placement(transformation(origin = {0, 20}, extent = {{-10, -10}, {10, 10}})));
  Compartment occupied_a(c_start = f_a_start * n_tot / vol, vol = vol) annotation(
    Placement(transformation(origin = {-90, 10}, extent = {{-10, -10}, {10, 10}})));
  Compartment occupied_b(c_start = f_b_start * n_tot / vol, vol = vol) annotation(
    Placement(transformation(origin = {90, 10}, extent = {{-10, -10}, {10, 10}})));
equation
  connect(site_a, assoc_a.ligand) annotation(
    Line(points = {{-30, 74}, {-20, 74}, {-20, 14}, {-36, 14}}));
  connect(assoc_a.free, free.substance) annotation(
    Line(points = {{-36, 4}, {2, 4}, {2, 10}}));
  connect(occupied_a.substance, assoc_a.occupied) annotation(
    Line(points = {{-90, 0}, {-90, -8}, {-68, -8}, {-68, 10}, {-56, 10}}));
  connect(site_b, assoc_b.ligand) annotation(
    Line(points = {{105, 100}, {26, 100}, {26, 15}, {36, 15}}));
  connect(free.substance, assoc_b.free) annotation(
    Line(points = {{2, 10}, {2, 4}, {36, 4}}));
  connect(assoc_b.occupied, occupied_b.substance) annotation(
    Line(points = {{56, 10}, {70, 10}, {70, -8}, {90, -8}, {90, 0}}));
  annotation(
    Icon(graphics = {Line(origin = {94, 99}, points = {{-6, -5}, {-2, -3}, {3, -8}, {10, -11}, {16, -7}}, thickness = 0.5), Text(origin = {-110, 0}, extent = {{-100, -10}, {100, 10}}, textString = &quot;%name&quot;, rotation = 90)}));
end Buffer2;</code></pre>
  <p>This model can be used as component in models that need to convert
  between chemical ion flow rates and the current introduced through this flow.
  If a defining equation for <code>i</code> is provided, <code>rate</code>
  will contain the corresponding ion flow rate and vice versa.</p>
<pre><code class="language-modelica">model ECAdapter &quot;adapter between electrical current and chemical substance flow rate&quot;
  extends InaMo.Icons.Adapter;
  SI.Current i &quot;current due to substance transport&quot;;
  SI.MolarFlowRate rate &quot;flow rate of substance transport&quot;;
  parameter Real n(unit = &quot;1&quot;) &quot;stoichiometric ratio of ion transport&quot;;
  parameter Integer z &quot;valence of ion&quot;;
equation
  rate = n * i / (z * Modelica.Constants.F);
  annotation(
    Documentation(info = &quot;&lt;html&gt;
  &lt;p&gt;This model can be used as component in models that need to convert
  between chemical ion flow rates and the current introduced through this flow.
  If a defining equation for &lt;code&gt;i&lt;/code&gt; is provided, &lt;code&gt;rate&lt;/code&gt;
  will contain the corresponding ion flow rate and vice versa.&lt;/p&gt;
&lt;/html&gt;&quot;));
end ECAdapter;</code></pre><h2 id="Ca2-handling"><a class="docs-heading-anchor" href="#Ca2-handling">Ca2+ handling</a><a id="Ca2-handling-1"></a><a class="docs-heading-anchor-permalink" href="#Ca2-handling" title="Permalink"></a></h2><pre><code class="language-modelica">model RyanodineReceptor &quot;ryanodine receptor kinetics for Ca2+ release by the SR&quot;
  extends InaMo.Icons.InsideBottomOutsideTop;
  extends InaMo.Icons.LipidBilayerWithGap;
  extends InaMo.Icons.Activatable;
  extends InaMo.Concentrations.Interfaces.InactiveChemicalTransport;
  extends InaMo.Icons.Current(current_name = &quot;RyR&quot;);
  parameter Real p(quantity = &quot;reaction rate coefficient&quot;, unit = &quot;1/s&quot;) &quot;rate coefficient (inverse of time constant)&quot;;
  parameter SI.Concentration ka &quot;concentration producing half occupation&quot;;
  parameter Real n(unit = &quot;1&quot;) &quot;Hill coefficient&quot;;
equation
  coeff = p * hillLangmuir(dst.amount / vol_dst, ka, n);
end RyanodineReceptor;</code></pre><pre><code class="language-modelica">model SERCAPump &quot;SERCA kinetics for Ca2+ uptake by the SR&quot;
  extends InaMo.Icons.InsideTopOutsideBottom;
  extends InaMo.Concentrations.Interfaces.SubstanceTransport;
  extends InaMo.Icons.LipidBilayerWithGap;
  extends InaMo.Icons.SERCA;
  extends InaMo.Icons.Current(current_name = &quot;SERCA&quot;);
  parameter SI.Volume vol_src &quot;volume of source compartment&quot;;
  parameter SI.MolarFlowRate p &quot;maximum flow rate&quot;;
  parameter SI.Concentration k &quot;Michaelis constant&quot;;
equation
  rate = p * michaelisMenten(src.amount / vol_src, k);
end SERCAPump;</code></pre>
    <p>Kurata et al. model the intracellular calcium concentration based on
    four compartments:</p>
    <ul>
      <li>the cytoplasm (cyto)</li>
      <li>the junctional sarcoplasmic reticulum (JSR)</li>
      <li>the network sarcoplasmic reticulum (NSR)</li>
      <li>the &quot;subspace&quot; subspace (sub), i.e. the &quot;functionally
      restricted intracellular space accessible to the NaCa exchanger as well
      as to the L-type Ca2+ channel and the Ca2+-gated Ca2+ channel in the SR.
      </li>
    </ul>
    <p>The model includes the transfer between these compartments by
    diffusion reactions (cytoplasm <-> subspace and JSR <-> NSR), the uptake
    of Ca2+ in the SR via the SERCA pump, and the release of Ca2+ from the
    SR via the ryanodine receptors.
    Additionally, the effect of several Ca2+ buffers is modeled:</p>
    <ul>
      <li>troponin-Ca</li>
      <li>troponin-Mg</li>
      <li>calmodulin (in subspace and cytosol)</li>
      <li>calsequestrin</li>
    </ul>
  <pre><code class="language-modelica">model CaHandlingK &quot;handling of Ca concentation by Kurata 2002&quot;
  extends InaMo.Icons.SarcoplasmicReticulum;
  parameter SI.Concentration tc_tot = 0.031 &quot;total concentration of troponin-Ca&quot;;
  parameter SI.Concentration tmc_tot = 0.062 &quot;total concentration of troponin-Mg binding to Ca2+&quot;;
  parameter SI.Concentration cm_tot = 0.045 &quot;total concentration of calmodulin&quot;;
  parameter SI.Concentration cq_tot = 10 &quot;total concentration of calsequestrin&quot;;
  outer parameter SI.Volume v_sub &quot;volume of subspace&quot;;
  outer parameter SI.Volume v_cyto &quot;volume of cytosol&quot;;
  outer parameter SI.Volume v_nsr &quot;volume of network SR&quot;;
  outer parameter SI.Volume v_jsr &quot;volume of junctional SR&quot;;
  InaMo.Concentrations.Interfaces.SubstanceSite ca_sub &quot;connector exposing Ca2+ in subspace to external influences by membrane currents&quot; annotation(
    Placement(transformation(origin = {-100, 0}, extent = {{-17, -17}, {17, 17}})));
  InaMo.Concentrations.Basic.ConstantConcentration mg(c_const = 2.5, vol = v_cyto) &quot;Mg2+ concentration&quot; annotation(
    Placement(transformation(origin = {80, -26}, extent = {{-17, -17}, {17, 17}})));
  InaMo.Concentrations.Basic.Compartment sub(vol = v_sub) &quot;Ca2+ in subspace&quot; annotation(
    Placement(transformation(origin = {-86, 82}, extent = {{-17, -17}, {17, 17}})));
  InaMo.Concentrations.Basic.Compartment cyto(vol = v_cyto) &quot;Ca2+ in cytosol&quot; annotation(
    Placement(transformation(origin = {20, -28}, extent = {{-17, -17}, {17, 17}})));
  InaMo.Concentrations.Basic.Compartment jsr(vol = v_jsr) &quot;Ca2+ in JSR&quot; annotation(
    Placement(transformation(origin = {-16, 70}, extent = {{-17, -17}, {17, 17}})));
  InaMo.Concentrations.Basic.Compartment nsr(vol = v_nsr) &quot;Ca2+ in NSR&quot; annotation(
    Placement(transformation(origin = {62, 54}, extent = {{-17, -17}, {17, 17}})));
  InaMo.Concentrations.Basic.Diffusion sub_cyto(vol_src = sub.vol, vol_dst = cyto.vol, tau = 0.04e-3) &quot;diffusion from subspace to cytosol&quot; annotation(
    Placement(transformation(origin = {-58, -38}, extent = {{17, -17}, {-17, 17}}, rotation = -90)));
  // tau = tau_diff,Ca
  InaMo.Concentrations.Atrioventricular.SERCAPump cyto_nsr(vol_src = cyto.vol, p = 0.005e3 * v_nsr, k = 0.0006) &quot;transport from cytosol to NSR via SERCA&quot; annotation(
    Placement(transformation(origin = {48, 12}, extent = {{-17, -17}, {17, 17}})));
  // p = P_up, k = K_up
  InaMo.Concentrations.Basic.Diffusion nsr_jsr(vol_src = nsr.vol, vol_dst = jsr.vol, tau = 60e-3) &quot;diffusion from NSR to JSR&quot; annotation(
    Placement(transformation(origin = {16, 42}, extent = {{-17, -17}, {17, 17}}, rotation = 90)));
  // tau = tau_tr
  InaMo.Concentrations.Atrioventricular.RyanodineReceptor jsr_sub(vol_src = jsr.vol, vol_dst = sub.vol, p = 5e3, ka = 0.0012, n = 2) &quot;transport from JSR to subspace via RyR&quot; annotation(
    Placement(transformation(origin = {-50, 62}, extent = {{-17, -17}, {17, 17}}, rotation = 90)));
  // p = P_rel, k = K_rel
  InaMo.Concentrations.Basic.Buffer tc(n_tot = tc_tot * v_cyto, k = 88.8e3 / v_cyto, kb = 0.446e3) &quot;troponin-Ca&quot; annotation(
    Placement(transformation(origin = {-36, -74}, extent = {{-17, -17}, {17, 17}})));
  InaMo.Concentrations.Basic.Buffer2 tm(n_tot = tmc_tot * v_cyto, vol = v_cyto, k_a = 227.7e3 / v_cyto, kb_a = 0.00751e3, k_b = 2.277e3 / v_cyto, kb_b = 0.751e3) &quot;troponin-mg&quot; annotation(
    Placement(transformation(origin = {46, -72}, extent = {{-17, -17}, {17, 17}})));
  InaMo.Concentrations.Basic.Buffer cm_cyto(n_tot = cm_tot * v_cyto, k = 227.7e3 / v_cyto, kb = 0.542e3) &quot;calmodulin in cytosol&quot; annotation(
    Placement(transformation(origin = {4, -74}, extent = {{-17, -17}, {17, 17}})));
  InaMo.Concentrations.Basic.Buffer cm_sub(n_tot = cm_tot * v_sub, k = cm_cyto.k * v_cyto / v_sub, kb = cm_cyto.kb) &quot;calmodulin in subspace&quot; annotation(
    Placement(transformation(origin = {-74, 28}, extent = {{-17, -17}, {17, 17}})));
  InaMo.Concentrations.Basic.Buffer cq(n_tot = cq_tot * v_jsr, k = 0.534e3 / v_jsr, kb = 0.445e3) &quot;calsequestrin&quot; annotation(
    Placement(transformation(origin = {-18, 18}, extent = {{-17, -17}, {17, 17}})));
equation
  connect(sub.substance, ca_sub) annotation(
    Line(points = {{-86, 66}, {-96, 66}, {-96, 0}, {-100, 0}}));
  connect(ca_sub, sub_cyto.src) annotation(
    Line(points = {{-100, 0}, {-96, 0}, {-96, -38}, {-74, -38}, {-74, -38}}));
  connect(sub_cyto.dst, cyto.substance) annotation(
    Line(points = {{-40, -38}, {0, -38}, {0, -44}, {20, -44}, {20, -44}}));
  connect(cyto.substance, cyto_nsr.src) annotation(
    Line(points = {{20, -44}, {48, -44}, {48, -4}, {48, -4}}));
  connect(cyto_nsr.dst, nsr.substance) annotation(
    Line(points = {{48, 30}, {62, 30}, {62, 38}, {62, 38}}));
  connect(nsr.substance, nsr_jsr.src) annotation(
    Line(points = {{62, 38}, {40, 38}, {40, 42}, {34, 42}, {34, 42}}));
  connect(nsr_jsr.dst, jsr.substance) annotation(
    Line(points = {{0, 42}, {-16, 42}, {-16, 54}, {-16, 54}}));
  connect(jsr.substance, jsr_sub.src) annotation(
    Line(points = {{-16, 54}, {-32, 54}, {-32, 62}, {-32, 62}}));
  connect(jsr_sub.dst, sub.substance) annotation(
    Line(points = {{-66, 62}, {-86, 62}, {-86, 66}, {-86, 66}}));
  connect(tc.site, cyto.substance) annotation(
    Line(points = {{-40, -62}, {-40, -62}, {-40, -48}, {20, -48}, {20, -44}}));
  connect(tm.site_a, cyto.substance) annotation(
    Line(points = {{42, -60}, {42, -48}, {20, -48}, {20, -44}}));
  connect(tm.site_b, mg.substance) annotation(
    Line(points = {{66, -55}, {66, -48}, {80, -48}, {80, -42}}));
  connect(cm_cyto.site, cyto.substance) annotation(
    Line(points = {{0, -62}, {0, -48}, {20, -48}, {20, -44}}));
  connect(sub.substance, cm_sub.site) annotation(
    Line(points = {{-86, 66}, {-86, 66}, {-86, 48}, {-78, 48}, {-78, 40}, {-78, 40}}));
  connect(jsr.substance, cq.site) annotation(
    Line(points = {{-16, 54}, {-16, 54}, {-16, 30}, {-22, 30}, {-22, 30}}));
  annotation(
    Documentation(info = &quot;&lt;html&gt;
    &lt;p&gt;Kurata et al. model the intracellular calcium concentration based on
    four compartments:&lt;/p&gt;
    &lt;ul&gt;
      &lt;li&gt;the cytoplasm (cyto)&lt;/li&gt;
      &lt;li&gt;the junctional sarcoplasmic reticulum (JSR)&lt;/li&gt;
      &lt;li&gt;the network sarcoplasmic reticulum (NSR)&lt;/li&gt;
      &lt;li&gt;the &amp;quot;subspace&amp;quot; subspace (sub), i.e. the &amp;quot;functionally
      restricted intracellular space accessible to the NaCa exchanger as well
      as to the L-type Ca2+ channel and the Ca2+-gated Ca2+ channel in the SR.
      &lt;/li&gt;
    &lt;/ul&gt;
    &lt;p&gt;The model includes the transfer between these compartments by
    diffusion reactions (cytoplasm &lt;-&gt; subspace and JSR &lt;-&gt; NSR), the uptake
    of Ca2+ in the SR via the SERCA pump, and the release of Ca2+ from the
    SR via the ryanodine receptors.
    Additionally, the effect of several Ca2+ buffers is modeled:&lt;/p&gt;
    &lt;ul&gt;
      &lt;li&gt;troponin-Ca&lt;/li&gt;
      &lt;li&gt;troponin-Mg&lt;/li&gt;
      &lt;li&gt;calmodulin (in subspace and cytosol)&lt;/li&gt;
      &lt;li&gt;calsequestrin&lt;/li&gt;
    &lt;/ul&gt;
  &lt;/html&gt;&quot;),
    Diagram(graphics = {Polygon(origin = {12, 0}, fillColor = {213, 213, 213}, pattern = LinePattern.None, fillPattern = FillPattern.Solid, points = {{-68, -100}, {-70, -74}, {-70, -54}, {-70, -16}, {-62, 8}, {-66, 100}, {-44, 100}, {88, 100}, {88, 100}, {88, -100}, {88, -100}, {-51, -100}, {-68, -100}}, smooth = Smooth.Bezier), Polygon(origin = {-90, 142}, points = {{171.31, -71}, {157.99, -71}, {115.86, -71}, {105.58, -71}, {105.58, -49.41}, {40.28, -49.41}, {40.28, -77}, {40.28, -121}, {40.28, -148.59}, {103.58, -148.59}, {103.58, -129}, {115.86, -129}, {157.99, -129}, {171.31, -129}, {171.31, -108}, {171.31, -92}, {171.31, -71}}, smooth = Smooth.Bezier)}));
end CaHandlingK;</code></pre>
  <p>This model is an extension to the Ca2+ handling in Kurata 2002 by Inada
  et al..
  It includes an additional calmodulin concentration in the sarcolemma.</p>
  <p>NOTE: Unfortunately, the total value of this concentration (cm_sl_tot) is not
  given in Inada et al. (where it is called SL_tot).
  In the C++ implementation by Inada et al., cm_sl_tot has a value of
  31/1.2 mM (av_node_2.cpp:508), which seems to be the wrong order of magnitude
  compared to, the other calmodulin concentrations which use a total
  concentration of 0.045 mM.
  This is probably the reason why the C++ code multiplies the whole equation
  for der(cm_sl.f) with 0.0001, effectively reducing cm_sl.k and cm_sl.kb and
  thus cancelling out the increase in cm_sl_tot by the same factor.
  The CellML version attempts to correct the value for cm_sl_tot, but keeps
  the already corrected values for cm_sl.k and cm_sl.kb, effectively
  introducing an error.
  In InaMo, we use a reduced cm_sl_tot, but also increase cm_sl.k and cm_sl.kb
  accordingly to achieve the same numerical result as the C++ code.</p>
<pre><code class="language-modelica">model CaHandling &quot;extension of Ca handling by Inaada 2009&quot;
  extends CaHandlingK;
  parameter SI.Concentration cm_sl_tot = 0.031 / 1.2 &quot;total concentration of calmodulin in sarcolemma&quot;;
  InaMo.Concentrations.Basic.Buffer cm_sl(n_tot = cm_sl_tot * v_sub, k = 0.115e3 / v_sub, kb = 1e3) &quot;calmodulin in sarcolemma&quot; annotation(
    Placement(transformation(origin = {-82, -78}, extent = {{-17, -17}, {17, 17}})));
equation
  connect(cm_sl.site, sub.substance) annotation(
    Line(points = {{-86, -66}, {-86, -66}, {-86, -56}, {-96, -56}, {-96, 0}, {-100, 0}}));
  annotation(
    Documentation(info = &quot;&lt;html&gt;
  &lt;p&gt;This model is an extension to the Ca2+ handling in Kurata 2002 by Inada
  et al..
  It includes an additional calmodulin concentration in the sarcolemma.&lt;/p&gt;
  &lt;p&gt;NOTE: Unfortunately, the total value of this concentration (cm_sl_tot) is not
  given in Inada et al. (where it is called SL_tot).
  In the C++ implementation by Inada et al., cm_sl_tot has a value of
  31/1.2 mM (av_node_2.cpp:508), which seems to be the wrong order of magnitude
  compared to, the other calmodulin concentrations which use a total
  concentration of 0.045 mM.
  This is probably the reason why the C++ code multiplies the whole equation
  for der(cm_sl.f) with 0.0001, effectively reducing cm_sl.k and cm_sl.kb and
  thus cancelling out the increase in cm_sl_tot by the same factor.
  The CellML version attempts to correct the value for cm_sl_tot, but keeps
  the already corrected values for cm_sl.k and cm_sl.kb, effectively
  introducing an error.
  In InaMo, we use a reduced cm_sl_tot, but also increase cm_sl.k and cm_sl.kb
  accordingly to achieve the same numerical result as the C++ code.&lt;/p&gt;
&lt;/html&gt;&quot;));
end CaHandling;</code></pre></article><nav class="docs-footer"><a class="docs-footer-prevpage" href="../channels/">« Ion channels</a><a class="docs-footer-nextpage" href="../pumps/">Ion pumps »</a><div class="flexbox-break"></div><p class="footer-message">Powered by <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> and the <a href="https://julialang.org/">Julia Programming Language</a>.</p></nav></div><div class="modal" id="documenter-settings"><div class="modal-background"></div><div class="modal-card"><header class="modal-card-head"><p class="modal-card-title">Settings</p><button class="delete"></button></header><section class="modal-card-body"><p><label class="label">Theme</label><div class="select"><select id="documenter-themepicker"><option value="documenter-light">documenter-light</option><option value="documenter-dark">documenter-dark</option></select></div></p><hr/><p>This document was generated with <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> on <span class="colophon-date" title="Friday 12 February 2021 13:32">Friday 12 February 2021</span>. Using Julia version 1.5.3.</p></section><footer class="modal-card-foot"></footer></div></div></div></body></html>
